plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.19.0'
}

group = 'com.likelion'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Swagger openapi-ui
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.8.1'

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // DB
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Spring-security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // Jwt Token
    implementation group:'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    implementation group:'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    implementation group:'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
}

tasks.named('compileJava') {
    dependsOn 'spotlessApply'
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        // Google JAVA Format 적용
        googleJavaFormat()
        // 아래 순서로 import 문 정렬
        importOrder('java', 'javax', 'jakarta', 'org', 'com')
        // 사용하지 않는 import 제거
        removeUnusedImports()
        // 각 라인 끝에 있는 공백을 제거
        trimTrailingWhitespace()
        // 파일 끝에 새로운 라인 추가
        endWithNewline()
    }
}

processResources {
    dependsOn 'copyResources'
}

tasks.register('copyResources', Copy) {
    from './BackEnd_Config'
    include '*.properties'
    into 'src/main/resources/'
}

test {
    doFirst {
        def profile = project.hasProperty('profile') ? project.property('profile').toString() : 'local'
        jvmArgs = ["-Dspring.profiles.active=${profile}"]
    }
}